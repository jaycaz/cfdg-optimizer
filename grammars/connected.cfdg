startshape go1

CF::Background = [brightness -0.9]

shape go1
{
	go [hue 20 brightness 0.75 saturation 0.5]
}

shape go
{
	loop 2 [rotate 180] {
		start []
	}
}

shape start
rule 2 {
	start [rotate 1 hue 1]
}

rule 2 {
	start [rotate -1 hue -1]
}

rule {
	streak []
}

shape streak
rule 10000 {
  mark []
	streak [
		rotate 0.75 y 0.25 z -0.001 size 0.999 brightness -0.0001 saturation 0.01
	]
}

rule * 20 {
  start [rotate -60 brightness 0.5 size 0.9]
	start [rotate  60 brightness 0.5 size 0.9]
}

rule * 1 {
	streak [flip 90]
}

rule * 10 {
  implode [hue 170 brightness 0.5 alpha -1 z -10]
}

shape implode
{
  CIRCLE [size 10]
  implode [brightness 0.01 size 0.99 alpha 0.0001]
}

shape mark
{
	CIRCLE []
}

